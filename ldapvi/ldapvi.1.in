.SH GETTING STARTED
Assuming a suitably configured LDAP library, run ldapvi without arguments
to see all entries available. 

Otherwise, try `ldapvi -h HOSTNAME --discover' to query the ROOT DSE for
available naming contexts.

Once that works, run `ldapvi -h HOSTNAME --discover --config' to
generate sample configuration that can be pasted into ~/.ldaprc or
/etc/ldap/ldap.conf.

.SH FILE FORMAT
ldapvi uses an LDIF-like syntax, but not standard LDIF.  For a precise
definition, see `ldapvi-file' below.  LDIF is defined in RFC 2849.

RFC 2849 modifies the language generated by its BNF grammar with the
"Notes on LDIF Syntax" (2) and (3) presented there.  These notes do not
apply to ldapvi syntax as stated: Line folding and comments can be used,
but are allowed specificially by our modified BNF, not hacked into the
rules later.  Line folding is allowed only in LDIF-compatible attribute
values and comments.  ldapvi files cannot be read line-by-line without
knowing the encoding of those lines, since newlines are permitted in
certain attribute value representations.

The following rules are used as defined in RFC 2849 and not reproduced
here: AttributeDescription, SAFE-STRING, BASE64-STRING, SPACE.
.SH Changes to the RFC 2849 BNF grammar
.nf
ldapvi-file = vspace ldapvi-record
              *(vspace SEP vspace ldapvi-record) vspace
ldapvi-record = ldapvi-attrval-record
	        / ldapvi-rename-record
		/ ldapvi-modify-record
ldapvi-attrval-record = *(comment) dn-spec 1*(*(comment) attrval-spec)
ldapvi-rename-record = *(comment)
                       "rename" distinguishedName SEP
		       ("add" / "replace") value-spec SEP
ldapvi-modify-record = *(comment)
                       "modify" distinguishedName SEP
		       1*(*(comment) mod-spec)
mod-spec = ("delete" / "add" / "replace") ad-value SEP
	   *(value-enc SEP)

vspace = *(comment / sep)
comment = "#" *(unescaped-char) *(SEP " " *(unescaped-char)) SEP

dn-spec = key distinguishedName SEP
key = number / "add"
distinguishedName = value-spec                  ;encoding an RFC 2253 DN

attrval-spec = AttributeDescription value-spec SEP
value-spec = value-simple / value-enc
value-simple = SPACE escaped-string             ;escape CR/LF with '\\\\'
value-enc = (":;" SPACE escaped-string /        ;ditto
             ":" SPACE 0*1(SAFE-STRING          ;LDIF compatible
                           *(SEP " " *SAFE-CHAR)) /
             "::" SPACE BASE64-STRING /         ;ditto
             ":" number SPACE 0*1(octet) /      ;exactly `number' octets
             ":<" SPACE url /                   ;only file:// supported
             ":crypt" SPACE password /          ;for userPassword
             ":cryptmd5" SPACE password /
             ":md5" SPACE password /
             ":smd5" SPACE password /
             ":sha" SPACE password /
             ":ssha" SPACE password /
             ;; other encoding markers reserved
            )

octet = %x00-ff
number = 1*(%x30-39)                            ;any decimal number
unescaped-char = %x00-%x09 / %x0a-0c / %x0d-ff  ;any except for CR/LF.
escaped-string = *(unescaped-char / %x5c %x0a / %x5c %x0d / %x5c %x5c)
password = 0*unescaped-char
SEP = LF                                        ;this is a unix program!

ad-value = value-spec  ;parsing as an AttributeDescription

.SH BUGS
Please report bugs to <ldapvi@lists.askja.de>.

.SH AUTHOR
David Lichteblau <david@lichteblau.com>
