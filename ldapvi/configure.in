AC_INIT(,, BUG-REPORT-ADDRESS)

dnl FIXME: With --enable-debug, quite some warnings about missing
dnl function prototypes are printed.  Most (but not all?) of them go
dnl away with -D_BSD_SOURCE.  However, _BSD_SOURCE appear to be some
dnl kind of Linuxism, and I am not at all sure why we need it in
dnl this case.
AC_ARG_ENABLE([debug],[  --enable-debug          enable debugging], [
	if test -n "$CFLAGS"; then
		AC_MSG_WARN([ignoring CFLAGS because of --debug])
	fi
	CFLAGS="-std=c89 -pedantic -g -Wall -Wno-parentheses"
])

if test -z "$CFLAGS"; then
	CFLAGS=-Wno-parentheses
fi

AC_PROG_INSTALL

# port.c
AC_CHECK_FUNCS([mkdtemp])
AC_CHECK_FUNCS([on_exit])

# solaris
AC_CHECK_LIB([socket],[main])
AC_CHECK_LIB([resolv],[main])

# traditional libldap isn't enough
AC_CHECK_LIB([lber],[main])
AC_CHECK_LIB([ldap],[main],:,AC_MSG_ERROR([libldap not found]))
AC_CHECK_LIB([ldap],[ldap_initialize],,AC_MSG_ERROR([libldap present but obsolete]))
AC_CHECK_LIB([ldap],[ldap_bv2dn_x],AC_DEFINE(LIBLDAP22),AC_DEFINE(LIBLDAP21))

# terminfo
AC_SEARCH_LIBS([tigetstr],[curses ncurses],,AC_MSG_ERROR([libcurses not found]))
AC_CHECK_HEADER([curses.h],,AC_MSG_ERROR([curses.h not found]))

# popt
AC_CHECK_LIB([popt],[poptGetContext],,AC_MSG_ERROR([libpopt not found]))

# pkg-config
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then AC_MSG_ERROR([pkg-config not found]); fi

# glib
LIBS="`$PKG_CONFIG --libs glib-2.0` $LIBS"
CFLAGS="`$PKG_CONFIG --cflags glib-2.0` $CFLAGS"
AC_CHECK_LIB([glib-2.0],[main],:,AC_MSG_ERROR([libglib2.0 not found]))

# libcrypto
AC_ARG_WITH(libcrypto, [  --with-libcrypto=(openssl|gnutls)
                          default is openssl],,with_libcrypto=openssl)

case $with_libcrypto in
	openssl)
		LIBS="`$PKG_CONFIG --libs openssl` $LIBS"
		CFLAGS="`$PKG_CONFIG --cflags openssl` $CFLAGS"
		AC_CHECK_LIB([ssl],[main],,AC_MSG_ERROR([OpenSSL not found]))
		AC_DEFINE(HAVE_OPENSSL)
		;;
	gnutls)
		LIBS="`$PKG_CONFIG --libs gnutls` $LIBS"
		CFLAGS="`$PKG_CONFIG --cflags gnutls` $CFLAGS"
		AC_CHECK_LIB([gnutls],[main],,AC_MSG_ERROR([GnuTLS not found]))
		AC_CHECK_LIB([gnutls-openssl],[main],,AC_MSG_ERROR([libgnutls-openssl not found]))
		AC_DEFINE(HAVE_GNUTLS)
		AC_DEFINE(SHA_DIGEST_LENGTH,20)
		;;
	*)
		AC_MSG_ERROR([invalid value --with-libcrypto=$with_libcrypto, expected openssl or gnutls])
		;;
esac
AC_CHECK_FUNCS([SHA1])
AC_CHECK_FUNCS([RAND_pseudo_bytes])

AC_CHECK_LIB([crypt],[main])

AC_ARG_WITH(dummy,[
Set PKG_CONFIG_PATH to choose a glib installation.])

AC_CONFIG_HEADERS([config.h])
AC_CONFIG_FILES([GNUmakefile])
AC_OUTPUT
